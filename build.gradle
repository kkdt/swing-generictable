/**
 * Copyright (C) 2018 thinh ho
 * This file is part of 'swing-generictable' which is released under the MIT license.
 * See LICENSE at the project root directory.
 */
buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
}

description = 'swing-generictable'
group = 'kkdt.generictable'
version = 1.0

repositories {
    mavenCentral()
}

ext {
    localrepo = project.hasProperty('localrepo') ? localrepo : layout.buildDirectory.dir("artifacts")
}

jar {
    manifest {
        attributes(
            "Name": project.name,
            "Specification-Title": "swing-generictable",
            "Specification-Version": version,
            "Specification-Vendor": "kkdt",
            "Implementation-Title": "swing-generictable",
            "Implementation-Version": version,
            "Implementation-Vendor": "kkdt.github.io"
        )
    }
}

dependencies {
    testImplementation libs.junit
}

test {
    systemProperty 'java.awt.headless', true
}

publishing {
    publications {
        myPublication(MavenPublication) {
            // publish jar archive
            from components.java
            // include publish README.md
            artifact ('README.md') {
                classifier = 'README'
                extension  = 'md'
            }
        }
    }
    // publish artifacts to local directory
    repositories {
       maven {
          url file(localrepo)
       }
    }
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
